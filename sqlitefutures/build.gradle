buildscript {
  dependencies {
    classpath rootProject.ext.androidPlugin
    classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.2'
    classpath 'com.github.dcendents:android-maven-plugin:1.2'
  }
}

apply plugin: 'com.android.library'

dependencies {
  androidTestCompile rootProject.ext.supportTestRunner
  androidTestCompile(rootProject.ext.truth) {
    exclude group: 'junit' // Android has JUnit built in.
  }
}

def isCi() {
  return "true".equals(System.getenv('CI'))
}

ext {
  bintrayRepo = 'maven'
  bintrayName = 'sqlitefutures'
 
  publishedGroupId = 'com.dannysu.sqlitefutures'
  libraryName = 'SQLiteFutures'
  artifact = 'sqlitefutures'
 
  libraryDescription = 'Helper classes for dealing with SQLite on Android'
 
  siteUrl = 'https://github.com/dannysu/sqlitefutures'
  gitUrl = 'https://github.com/dannysu/sqlitefutures'
 
  libraryVersion = '0.1.0'
 
  developerId = 'dannysu'
  developerName = 'Danny Su'
  developerEmail = 'contact@dannysu.com'

  licenseName = 'MIT'
  licenseUrl = 'http://opensource.org/licenses/MIT' 
  allLicenses = ['MIT']
}

android {
  compileSdkVersion rootProject.ext.compileSdkVersion
  buildToolsVersion rootProject.ext.buildToolsVersion

  defaultConfig {
    testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_7
    targetCompatibility JavaVersion.VERSION_1_7
  }

  dexOptions {
    preDexLibraries = !isCi()
  }

  lintOptions {
    textOutput 'stdout'
  }

  buildTypes {
    debug {
      testCoverageEnabled true
    }
  }
}

if (isCi()) {
  println 'Running on CI. Adding task to start emulator.'

  task startEmulator << {
    "$rootDir/gradle/start-emulator.sh".execute().waitFor()
  }

  android.testVariants.all { variant ->
    variant.connectedInstrumentTest.dependsOn startEmulator
  }
}

apply from: rootProject.file('gradle/gradle-mvn-push.gradle')

apply from: 'https://raw.githubusercontent.com/nuuneoi/JCenter/master/installv1.gradle'
apply from: 'https://raw.githubusercontent.com/nuuneoi/JCenter/master/bintrayv1.gradle'
